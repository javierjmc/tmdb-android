apply plugin: 'com.android.application'

android {
    compileSdkVersion vers.compileSdk
    buildToolsVersion vers.buildTools

    final BASE_APPLICATION_ID = 'com.example.themoviedb'

    defaultConfig {
        applicationId BASE_APPLICATION_ID
        minSdkVersion vers.minSdk
        targetSdkVersion vers.targetSdk
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        // used by Room, to test migrations
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    flavorDimensions "themoviedb"

    productFlavors {
        // The production flavored app uses the production api
        production {
            dimension "themoviedb"
            applicationId BASE_APPLICATION_ID
            testApplicationId applicationId + '.test'

            buildConfigField 'String', 'SERVER_URL', THEMOVIEDB_ENDPOINT
            buildConfigField 'String', 'SERVER_API_KEY', THEMOVIEDB_API_KEY
        }

        // The staging flavored app uses the staging api
        staging {
            dimension "themoviedb"
            applicationId BASE_APPLICATION_ID + ".staging"
            testApplicationId applicationId + '.test'

            buildConfigField 'String', 'SERVER_URL', THEMOVIEDB_ENDPOINT
            buildConfigField 'String', 'SERVER_API_KEY', THEMOVIEDB_API_KEY
        }
    }

    buildTypes {

        // The Debug builds are used for development and local testing
        debug {
            minifyEnabled false
            debuggable true
            applicationIdSuffix ".debug"
        }

        // The Release builds are used for publishing to play store
        release {
            minifyEnabled false
            //signingConfig signingConfigs.release
            //proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }

    // used by Room, to test migrations
    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }
}
/*
android.applicationVariants.all {
    def aptOutputDir = new File(buildDir, "generated/source/apt/${it.unitTestVariant.dirName}")
    it.unitTestVariant.addJavaSourceFoldersToModel(aptOutputDir)
}*/

dependencies {
    implementation "com.android.support:design:${vers.support}"
    implementation "com.android.support:appcompat-v7:${vers.support}"
    implementation "com.android.support:recyclerview-v7:${vers.support}"
    implementation "com.android.support:cardview-v7:${vers.support}"
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    // Joda-Time library
    // See: https://github.com/dlew/joda-time-android
    implementation 'net.danlew:android.joda:2.9.9.1'

    // Field and method binding for Android views
    // See: https://jakewharton.github.io/butterknife/
    implementation "com.jakewharton:butterknife:${vers.butterknife}"
    annotationProcessor "com.jakewharton:butterknife-compiler:${vers.butterknife}"

    // A Model-View-Presenter / Model-View-Intent library for modern Android apps
    // See: https://github.com/sockeqwe/mosby
    implementation "com.hannesdorfmann.mosby3:mvi:${vers.mosby}"

    implementation "io.reactivex.rxjava2:rxjava:${vers.rxjava2}"
    implementation "io.reactivex.rxjava2:rxandroid:${vers.rxandroid2}"

    // A logger with a small, extensible API which provides utility on top of Android's normal Log class.
    // See: https://github.com/JakeWharton/timber
    implementation "com.jakewharton.timber:timber:${vers.timber}"
    implementation "com.arcao:slf4j-timber:${vers.slf4j}"

    // A fast dependency injector for Android and Java.
    // See: https://github.com/google/dagger
    implementation "com.google.dagger:dagger:${vers.dagger}"
    implementation "com.google.dagger:dagger-android:${vers.dagger}"
    implementation "com.google.dagger:dagger-android-support:${vers.dagger}"
    provided 'javax.annotation:jsr250-api:1.0'
    annotationProcessor "com.google.dagger:dagger-compiler:${vers.dagger}"
    annotationProcessor "com.google.dagger:dagger-android-processor:${vers.dagger}"

    // A type-safe HTTP client for Android and Java
    // See: https://github.com/square/retrofit
    implementation "com.squareup.retrofit2:retrofit:${vers.retrofit}"
    implementation "com.squareup.retrofit2:adapter-rxjava2:${vers.retrofit}"
    implementation "com.squareup.retrofit2:converter-scalars:${vers.retrofit}"
    implementation "com.squareup.retrofit2:converter-moshi:${vers.retrofit}"

    // AutoValue: Moshi Extension
    // See: https://github.com/rharter/auto-value-moshi
    annotationProcessor 'com.ryanharter.auto.value:auto-value-moshi:0.4.4'
    provided 'com.ryanharter.auto.value:auto-value-moshi-annotations:0.4.4'

    // OkHttp: https://github.com/square/okhttp
    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.0'

    // An image loading and caching library for Android focused on smooth scrolling
    // See: https://github.com/bumptech/glide
    implementation "com.github.bumptech.glide:glide:${vers.glide}"
    annotationProcessor "com.github.bumptech.glide:compiler:${vers.glide}"

    implementation "com.jakewharton.rxbinding2:rxbinding:${vers.rxbinding}"
    implementation "com.jakewharton.rxbinding2:rxbinding-support-v4:${vers.rxbinding}"
    implementation "com.jakewharton.rxbinding2:rxbinding-design:${vers.rxbinding}"
    implementation "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7:${vers.rxbinding}"

    // Room Persistence
    // See: https://developer.android.com/training/data-storage/room/index.html
    implementation "android.arch.persistence.room:runtime:${vers.room}"
    annotationProcessor "android.arch.persistence.room:compiler:${vers.room}"
    implementation "android.arch.persistence.room:rxjava2:${vers.room}"


    testImplementation "junit:junit:${testvers.junit}"
    testImplementation "com.squareup.okhttp3:mockwebserver:${vers.okhttp}"
    testImplementation "org.mockito:mockito-core:${testvers.mockito}"
    testImplementation "org.mockito:mockito-android:${testvers.mockito}"
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
    androidTestImplementation "android.arch.persistence.room:testing:${vers.room}"

    // A debug bridge for Android applications
    // See: http://facebook.github.io/stetho/
    debugImplementation "com.facebook.stetho:stetho:${vers.stetho}"
    debugImplementation "com.facebook.stetho:stetho-okhttp3:${vers.stetho}"
    debugImplementation "com.facebook.stetho:stetho-urlconnection:${vers.stetho}"
    debugImplementation "com.facebook.stetho:stetho-js-rhino:${vers.stetho}"
    debugImplementation "com.facebook.stetho:stetho-timber:${vers.stetho}"
}
